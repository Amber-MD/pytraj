parm ../cpptraj/test/Test_Comprehensive/compound.prmtop
# system: 7mer peptide in a box of
# periodic water (1577 Waters)

# Read in the trajectory file 
# starting at 1
# 100 total snapshots
# consider each one 

trajin ../cpptraj/test/Test_Comprehensive/trajectory.mdcrd 1 100 1

#  first calculate some things that don't depend on
#  the position of the solute in the box

# do dihedral angles

dihedral phi :1@C  :2@N  :2@CA :2@C out phi
dihedral psi   :1@N  :1@CA :1@C  :2@N out psi
dihedral omega   :1@CA :1@C  :2@N  :2@CA out omega

# calculate the distance between two atoms 
# over the 10 snapshots
# output to the file "dist.list"

distance end_to_end :1@N :7@N out  dist_end_to_end.list

#get the rms values for the whole system 
#referenced to the first snapshot

rms first :1-1584

#now deal with drift

# the peptide (residues 1->7) has drifted around 
# the box during MD.  We'd like to view the system
# with a "constant" solute.

# note that this changes the coordinates you have in memory

# first center the solute by itself

center origin :1-7 

# now image the whole system about the centered origin

image origin center 

# calculate the waters in the first and second solvation shells
# (0-3.5A and 3.5-5.0A) and output to watershell.list 
 
watershell :1-7 watershell.list ws 

# save only the peptide for easy graphics viewing

strip :WAT

trajout test.mdcrd

